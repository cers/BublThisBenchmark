set xtics rotate by 45 right font ",8"
set ylabel "s/run (lower is better)"
set xlabel "Release date"
set title "BublThis benchmark (1200x800 image)"
set yrange [2:115]
# I really don't know why these numbers work
set xrange [260331852:449727000]
set xdata time
set format x "%b %Y"
set timefmt "%s"
set grid ytics
set ytics (1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,20,30,40,50,60,70,80,90,100)
set logscale y
set y2tics (1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,20,30,40,50,60,70,80,90,100)
set logscale y2

set key top right

set border linewidth 1.5
set style line 1 linecolor rgb '#e66000' linetype 1 linewidth 2
set style line 2 linecolor rgb '#3369e8' linetype 1 linewidth 2
set samples 5000,5000

set object 1 rect from screen 0, 0, 0 to screen 1, 1, 0 behind
set object 1 rect fc  rgb "white"  fillstyle solid 1.0
set terminal svg size 1650,850 fname 'Verdana' fsize 10
set output 'plots/bublthis-plot.svg'

plot 'data/bublthis-firefox.txt' using 1:($4/1000):3 with labels left offset 0.5,0.5 rotate by 45 notitle, '' using 1:($4/1000) with linespoints linestyle 1 title 'Firefox versions', 'data/bublthis-chrome.txt' using 1:($4/1000):6 with labels left offset 0.5,0.5 rotate by 45 notitle, '' using 1:($4/1000) with linespoints linestyle 2 title 'Chrome versions'